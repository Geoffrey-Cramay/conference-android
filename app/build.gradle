apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

buildscript {
    ext {
        kotlinVersion = '1.0.1-2'
        supportVersion = '23.2.1'
        retrofitVersion = '2.0.2'
        okhttpVersion = '3.1.0'
        daggerVersion = '2.0.2'
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

def getBuildNumber() {
    return System.getenv("CIRCLE_BUILD_NUM") as Integer ?: 1
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "com.arnaudpiroelle.conference"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode getBuildNumber()
        versionName "1.0.0"

        buildConfigField("String", "CONFERENCE_TIMEZONE", "\"Europe/Paris\"")
        buildConfigField("String", "CONFERENCE_NAME", "\"Build the future\"")
        buildConfigField("String", "CONFERENCE_START", "\"2015-11-04T08:00:00.000Z\"")
        buildConfigField("String", "CONFERENCE_END", "\"2015-11-04T19:00:00.000Z\"")
        buildConfigField("String", "CONFERENCE_BACKGROUND", "\"http://blog.xebia.fr/wp-content/uploads/2015/10/xebicon_background-11.png\"")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    productFlavors {
        local {
            buildConfigField 'String', 'API_ENDPOINT', "\"http://192.168.64.1:8989/\""
        }

        prod {
            buildConfigField 'String', 'API_ENDPOINT', "\"http://arnaudpiroelle.github.io/conference-android/api/\""
        }
    }

}

dependencies {
    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:design:$supportVersion"
    compile "com.android.support:cardview-v7:$supportVersion"
    compile "com.android.support:customtabs:$supportVersion"

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"

    compile 'com.jakewharton.timber:timber:4.1.1'

    compile "com.squareup.okhttp3:okhttp:$okhttpVersion"
    compile "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"

    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    compile "com.squareup.retrofit2:adapter-rxjava:$retrofitVersion"
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.0.2'

    compile "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    provided 'org.glassfish:javax.annotation:10.0-b28'

    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'io.reactivex:rxjava:1.1.0'

    compile 'com.squareup.sqlbrite:sqlbrite:0.6.2'

    compile 'com.facebook.stetho:stetho:1.3.1'
}

kapt {
    generateStubs = true
}
